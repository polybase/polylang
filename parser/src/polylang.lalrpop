use crate::ast::*;
use crate::lexer;
use std::str::FromStr;
use lalrpop_util::ParseError;

grammar<'input>(input: &'input str);

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Tok<'input> {
        identifier => lexer::Tok::Identifier(<&'input str>),
        number => lexer::Tok::NumberLiteral(<f64>),
        string => lexer::Tok::StringLiteral(<&'input str>),
        "desc" => lexer::Tok::Desc,
        "asc" => lexer::Tok::Asc,
        "true" => lexer::Tok::True,
        "false" => lexer::Tok::False,
        "string" => lexer::Tok::String,
        "number" => lexer::Tok::Number,
        "boolean" => lexer::Tok::Boolean,
        "map" => lexer::Tok::Map,
        "record" => lexer::Tok::Record,
        "PublicKey" => lexer::Tok::PublicKey,
        "let" => lexer::Tok::Let,
        "break" => lexer::Tok::Break,
        "return" => lexer::Tok::Return,
        "throw" => lexer::Tok::Throw,
        "if" => lexer::Tok::If,
        "else" => lexer::Tok::Else,
        "while" => lexer::Tok::While,
        "for" => lexer::Tok::For,
        "function" => lexer::Tok::Function,
        "@index" => lexer::Tok::Index,
        "collection" => lexer::Tok::Collection,
        "{" => lexer::Tok::LBrace,
        "}" => lexer::Tok::RBrace,
        "[" => lexer::Tok::LBracket,
        "]" => lexer::Tok::RBracket,
        "(" => lexer::Tok::LParen,
        ")" => lexer::Tok::RParen,
        "<" => lexer::Tok::ArrowLeft,
        ">" => lexer::Tok::ArrowRight,
        ":" => lexer::Tok::Colon,
        ";" => lexer::Tok::Semicolon,
        "," => lexer::Tok::Comma,
        "." => lexer::Tok::Dot,
        "!" => lexer::Tok::Bang,
        "?" => lexer::Tok::Question,
        "~" => lexer::Tok::Tilde,
        "*" => lexer::Tok::Star,
        "/" => lexer::Tok::Slash,
        "%" => lexer::Tok::Percent,
        "+" => lexer::Tok::Plus,
        "-" => lexer::Tok::Minus,
        "&" => lexer::Tok::Ampersand,
        "**" => lexer::Tok::StarStar,
        "&&" => lexer::Tok::AmpersandAmpersand,
        "@" => lexer::Tok::At,
        "^" => lexer::Tok::Caret,
        "|" => lexer::Tok::Pipe,
        "||" => lexer::Tok::PipePipe,
        "<=" => lexer::Tok::Lte,
        ">=" => lexer::Tok::Gte,
        "=" => lexer::Tok::Equal,
        "==" => lexer::Tok::EqualEqual,
        "!=" => lexer::Tok::BangEqual,
        "-=" => lexer::Tok::MinusEqual,
        "+=" => lexer::Tok::PlusEqual,
    }
}

Ident: String = {
    <i:identifier> => i.to_string(),
    "desc" => "desc".to_string(),
    "asc" => "asc".to_string(),
};

BasicType: Type = {
    "string" => Type::String,
    "number" => Type::Number,
    "boolean" => Type::Boolean,
};

Type: Type = {
    BasicType,
    <t:BasicType> "[" "]" => Type::Array(Box::new(t)),
    "map" "<" <kt:BasicType> "," <vt:Type> ">" => Type::Map(Box::new(kt), Box::new(vt)),
    "{" <fields:(Field ";")*> "}" => Type::Object(fields.into_iter().map(|(f, _)| f).collect()),
    "PublicKey" => Type::PublicKey,
};

ParameterType: ParameterType = {
    <l:@L> <t:Type> =>? match t {
        Type::String => Ok(ParameterType::String),
        Type::Number => Ok(ParameterType::Number),
        Type::Boolean => Ok(ParameterType::Boolean),
        Type::Array(t) => Ok(ParameterType::Array(*t)),
        Type::Map(kt, vt) => Ok(ParameterType::Map(*kt, *vt)),
        Type::Object(fields) => Err(ParseError::User {
            error: lexer::LexicalError::UserError {
                start: l,
                end: l,
                message: "object type not allowed for parameter".to_string(),
            }
        }),
        Type::PublicKey => Ok(ParameterType::PublicKey),
    },
    "record" => ParameterType::Record,
    <i:Ident> => ParameterType::ForeignRecord {
        collection: i,
    },
};

Number: f64 = {
    <n:number> => n,
};

String: String = {
    <s:string> => s[1..s.len()-1].to_string(),
};

Boolean: bool = {
    "true" => true,
    "false" => false,
};

Primitive: Primitive = {
    <n:Number> => Primitive::Number(n),
    <s:String> => Primitive::String(s),
};

ObjectFieldValues: Vec<(String, Expression)> = {
    <id:Ident> ":" <e:Expression> => vec![(id, e)],
    <id:Ident> ":" <e:Expression> "," <fs:ObjectFieldValues> => {
        let mut fs = fs;
        fs.insert(0, (id, e));
        fs
    },
};

pub Expression: Expression = {
    #[precedence(level="0")]
    <p:Primitive> => Expression::Primitive(p),
    #[precedence(level="0")]
    <b:Boolean> => Expression::Boolean(b),
    #[precedence(level="0")]
    <id:Ident> => Expression::Ident(id),
    #[precedence(level="0")]
    "{" <fields:ObjectFieldValues> "}" => Expression::Object(Object { fields }),
    #[precedence(level="1")]
    <l:Expression> "[" <r:Expression> "]" => Expression::Index(Box::new(l), Box::new(r)),
    #[precedence(level="1")]
    <l:Expression> "." <id:Ident> => Expression::Dot(Box::new(l), id),
    #[precedence(level="1")]
    <l:Expression> "(" <args:ArgumentList> ")" => Expression::Call(Box::new(l), args),
    #[precedence(level="1")]
    "(" <e:Expression> ")" => e,
    #[precedence(level="2")]
    "!" <l:Expression> => Expression::Not(Box::new(l)),
    #[precedence(level="2")]
    "~" <l:Expression> => Expression::BitNot(Box::new(l)),
    #[precedence(level="3")] #[assoc(side="left")]
    <l:Expression> "**" <r:Expression> => Expression::Exponent(Box::new(l), Box::new(r)),
    #[precedence(level="4")] #[assoc(side="left")]
    <l:Expression> "*" <r:Expression> => Expression::Multiply(Box::new(l), Box::new(r)),
    #[precedence(level="4")] #[assoc(side="left")]
    <l:Expression> "/" <r:Expression> => Expression::Divide(Box::new(l), Box::new(r)),
    #[precedence(level="4")] #[assoc(side="left")]
    <l:Expression> "%" <r:Expression> => Expression::Modulo(Box::new(l), Box::new(r)),
    #[precedence(level="5")] #[assoc(side="left")]
    <l:Expression> "+" <r:Expression> => Expression::Add(Box::new(l), Box::new(r)),
    #[precedence(level="5")] #[assoc(side="left")]
    <l:Expression> "-" <r:Expression> => Expression::Subtract(Box::new(l), Box::new(r)),
    #[precedence(level="6")] #[assoc(side="left")]
    <l:Expression> "<" <second:("<")?> <r:Expression> => match second {
        None => Expression::LessThan(Box::new(l), Box::new(r)),
        Some(_) => Expression::ShiftLeft(Box::new(l), Box::new(r)),
    },
    #[precedence(level="6")] #[assoc(side="left")]
    <l:Expression> ">" <second:(">")?> <r:Expression> => match second {
        None => Expression::GreaterThan(Box::new(l), Box::new(r)),
        Some(_) => Expression::ShiftRight(Box::new(l), Box::new(r)),
    },
    #[precedence(level="7")] #[assoc(side="left")]
    <l:Expression> "&" <r:Expression> => Expression::BitAnd(Box::new(l), Box::new(r)),
    #[precedence(level="8")] #[assoc(side="left")]
    <l:Expression> "^" <r:Expression> => Expression::BitXor(Box::new(l), Box::new(r)),
    #[precedence(level="9")] #[assoc(side="left")]
    <l:Expression> "|" <r:Expression> => Expression::BitOr(Box::new(l), Box::new(r)),
    #[precedence(level="10")] #[assoc(side="none")]
    <l:Expression> "<=" <r:Expression> => Expression::LessThanOrEqual(Box::new(l), Box::new(r)),
    #[precedence(level="10")] #[assoc(side="none")]
    <l:Expression> ">=" <r:Expression> => Expression::GreaterThanOrEqual(Box::new(l), Box::new(r)),
    #[precedence(level="11")] #[assoc(side="none")]
    <l:Expression> "==" <r:Expression> => Expression::Equal(Box::new(l), Box::new(r)),
    #[precedence(level="11")] #[assoc(side="none")]
    <l:Expression> "!=" <r:Expression> => Expression::NotEqual(Box::new(l), Box::new(r)),
    #[precedence(level="12")] #[assoc(side="left")]
    <l:Expression> "&&" <r:Expression> => Expression::And(Box::new(l), Box::new(r)),
    #[precedence(level="13")] #[assoc(side="left")]
    <l:Expression> "||" <r:Expression> => Expression::Or(Box::new(l), Box::new(r)),
    #[precedence(level="14")] #[assoc(side="left")]
    <l:Expression> "-=" <r:Expression> => Expression::AssignSub(Box::new(l), Box::new(r)),
    #[precedence(level="14")] #[assoc(side="left")]
    <l:Expression> "+=" <r:Expression> => Expression::AssignAdd(Box::new(l), Box::new(r)),
    #[precedence(level="14")] #[assoc(side="none")]
    <l:Expression> "=" <r:Expression> => Expression::Assign(Box::new(l), Box::new(r)),
};

ArgumentList: Vec<Expression> = {
    <e:Expression> <rest:("," Expression)*> => {
        let mut args = vec![e];
        for (_, e) in rest {
            args.push(e);
        }
        args
    },
    => vec![],
};

PrimitiveArgumentList: Vec<Primitive> = {
    <p:Primitive> <rest:("," Primitive)*> => {
        let mut args = vec![p];
        for (_, p) in rest {
            args.push(p);
        }
        args
    },
    => vec![],
};

CompoundStatement: Statement = {
    <i:If> => Statement::If(i),
    <w:While> => Statement::While(w),
    <f:For> => Statement::For(f),
};

Let: Let = {
    "let" <i:Ident> "=" <e:Expression> => Let { identifier: i, expression: e },
};

SmallStatement: Statement = {
    "break" => Statement::Break,
    "return" <e:Expression> => Statement::Return(e),
    "throw" <e:Expression> => Statement::Throw(e),
    <l:Let> => Statement::Let(l),
    <e:Expression> => Statement::Expression(e),
};

SimpleStatement: Statement = {
    <s:SmallStatement> ";" => s,
};

Statement: Statement = {
    SimpleStatement,
    CompoundStatement,
};

StatementsOrSimpleStatement: Vec<Statement> = {
    "{" <s:Statement*> "}" => s,
    <s:SimpleStatement> => vec![s],
};

If: If = {
    "if" "(" <e:Expression> ")" <s:StatementsOrSimpleStatement> <s2:("else" StatementsOrSimpleStatement)?> => If {
        condition: e,
        then_statements: s,
        else_statements: s2.map(|s| s.1).unwrap_or(vec![]),
    },
};

While: While = {
    "while" "(" <e:Expression> ")" "{" <s:Statement*> "}" => While {
        condition: e,
        statements: s,
    },
};

For: For = {
    "for" "(" <init:Let> ";" <cond:Expression> ";" <post:Expression> ")" "{" <statements:Statement*> "}" => For {
        initial_statement: ForInitialStatement::Let(init),
        condition: cond,
        post_statement: post,
        statements,
    },
    "for" "(" <init:Expression> ";" <cond:Expression> ";" <post:Expression> ")" "{" <statements:Statement*> "}" => For {
        initial_statement: ForInitialStatement::Expression(init),
        condition: cond,
        post_statement: post,
        statements,
    },
};

ParameterList: Vec<Parameter> = {
    <p:Parameter> <rest:("," Parameter)*> => {
        let mut params = vec![p];
        for (_, p) in rest {
            params.push(p);
        }
        params
    },
    => vec![],
};

Parameter: Parameter = {
    <name:Ident> ":" <type_:ParameterType> => Parameter {
        name,
        type_,
        required: true,
    },
    <name:Ident> "?" ":" <type_:ParameterType> => Parameter {
        name,
        type_,
        required: false,
    },
};

RootFunction: Function = {
    "function" <i: Ident> "(" <pl:ParameterList> ")" <return_type:(":" Type)?> "{" <l:@L> <s:Statement*> <r:@R> "}" => Function {
        name: i,
        decorators: vec![],
        parameters: pl,
        return_type: return_type.map(|(_, t)| t),
        statements: s,
        statements_code: input[l..r].to_string(),
    }
};

Function: Function = {
    <decs:Decorator*> "function" <i: Ident> "(" <pl:ParameterList> ")" <return_type:(":" Type)?> "{" <l:@L> <s:Statement*> <r:@R> "}" => Function {
        name: i,
        decorators: decs,
        parameters: pl,
        return_type: return_type.map(|(_, t)| t),
        statements: s,
        statements_code: input[l..r].to_string(),
    },
    <decs:Decorator*> <i: Ident> "(" <pl:ParameterList> ")" <return_type:(":" Type)?> "{" <l:@L> <s:Statement*> <r:@R> "}" => Function {
        name: i,
        decorators: decs,
        parameters: pl,
        return_type: return_type.map(|(_, t)| t),
        statements: s,
        statements_code: input[l..r].to_string(),
    },
};

Field: Field = {
    <decs:Decorator*> <name:Ident> "?" ":" <type_:Type> => Field{
        name,
        type_,
        required: false,
        decorators: decs,
    },
    <decs:Decorator*> <name:Ident> ":" <type_:Type> => Field{
        name,
        type_,
        required: true,
        decorators: decs,
    },
};

IndexField: IndexField = {
    "[" <path:FieldPath> "," <order:Order> "]" => IndexField{
        path,
        order,
    },
    <path:FieldPath> => IndexField{
        path,
        order: Order::Asc,
    },
};

FieldPath: Vec<String> = {
    <name:Ident> <rest:("." Ident)*> => {
        let mut path = vec![name];
        for (_, name) in rest {
            path.push(name);
        }
        path
    },
};

Order: Order = {
    "asc" => Order::Asc,
    "desc" => Order::Desc,
};

Index: Index = {
    "@index" "(" <fields:IndexFields> ")"  => Index{
        fields: fields,
    },
};

DecoratorName: String = {
    <l:@L> <name:Ident> =>? match name.as_str() {
        "public" => Ok(name),
        "read" => Ok(name),
        "call" => Ok(name),
        _ => Err(ParseError::User {
            error: lexer::LexicalError::UserError {
                start: l,
                end: l,
                message: format!("Unknown decorator: {}", name),
            },
        }),
    }
}

Decorator: Decorator = {
    "@" <name:DecoratorName> "(" <arguments:Ident*> ")" => Decorator{
        name,
        arguments,
    },
    "@" <name:DecoratorName> => Decorator{
        name,
        arguments: vec![],
    },
};

IndexFields: Vec<IndexField> = {
    <f:IndexField> <rest:("," IndexField)*> => {
        let mut fields = vec![f];
        for (_, f) in rest {
            fields.push(f);
        }
        fields
    },
    => vec![],
};

CollectionItem: CollectionItem = {
    <f:Field> ";" => CollectionItem::Field(f),
    <i:Index> ";" => CollectionItem::Index(i),
    <f:Function> => CollectionItem::Function(f),
};

Collection: Collection = {
    <decorators:Decorator*> "collection" <name:Ident> "{" <items:CollectionItem*> "}" => Collection {
        name: name,
        decorators: decorators,
        items: items,
    },
};

RootNode: RootNode = {
    <c:Collection> => RootNode::Collection(c),
    <f:RootFunction> => RootNode::Function(f),
};

pub Program: Program = {
    <r:RootNode*> => Program{
        nodes: r,
    },
};
